c khmer10.kmn
c 
c Khmer 1.0
c 
c Keyboard layout designed for Khmer.
c Based on the NiDA Khmer keyboard layout
c 
c 
c Andrew Cunningham    2006-05-20
c andrewc AT vicnet.net.au
c 

store(&VERSION) '9.0'
store(&NAME) "Khmer (NiDA)"
c BITMAP "khmer10.bmp"
store(&COPYRIGHT) "© 2006 State Library of Victoria"
store(&MESSAGE) "Unicode keyboard layout using the NiDA keyboard layout."
store(&VISUALKEYBOARD) 'khmer10.kvk'
store(&TARGETS) 'any windows'
store(&LAYOUTFILE) 'khmer10-layout.js'
store(&KEYBOARDVERSION) '1.3'
store(&BITMAP) 'khmer10.ico'
store(&ETHNOLOGUECODE) 'khm'
store(&LANGUAGE) 'x0453'
store(&WINDOWSLANGUAGES) 'x0453'

begin Unicode > use(main)

store(input12) "0123456789" \
               "abcdefghij" \
               "klmnopqrst" \
    	       "uvwxyz;=-." \
    	       "/`[\]'" \
    	       ")!@#$%^&*(" \
               "BCDEFGHIJK" \
               "LMNOPQRSTU" \
    	       "WXYZ+_>?~{" \
    	       "|}" '"'

store(level12) U+17E0 U+17E1 U+17E2 U+17E3 U+17E4 U+17E5 U+17E6 U+17E7 U+17E8 U+17E9 \
               U+17B6 U+1794 U+1785 U+178A U+17C1 U+1790 U+1784 U+17A0 U+17B7 U+17D2 \
               U+1780 U+179B U+1798 U+1793 U+17C4 U+1795 U+1786 U+179A U+179F U+178F \
    	       U+17BB U+179C U+17B9 U+1781 U+1799 U+178B U+17BE U+17B2 U+17A5 U+17D4 \
    	       U+17CA U+00AB U+17C0 U+17AE U+17AA U+17CB \ 
    	       U+0029 U+0021 U+17D7 U+0022 U+17DB U+0025 U+17CD U+17D0 U+17CF U+0028 \
               U+1796 U+1787 U+178C U+17C2 U+1792 U+17A2 U+17C7 U+17B8 U+1789 U+1782 \
               U+17A1 U+17C6 U+178E U+17C5 U+1797 U+1788 U+17AC U+17C3 U+1791 U+17BC \
    	       U+17BA U+1783 U+17BD U+178D U+003D U+17CC U+17D5 U+003F U+00BB U+17BF \
    	       U+17AD U+17A7 U+17C9 

store(input3) [CTRL ALT K_0] [CTRL ALT K_1] [CTRL ALT K_2] [CTRL ALT K_3] [CTRL ALT K_4] \ 
              [CTRL ALT K_5] [CTRL ALT K_6] [CTRL ALT K_7] [CTRL ALT K_8] [CTRL ALT K_9] \
              [CTRL ALT K_B] [CTRL ALT K_E] [CTRL ALT K_I] [CTRL ALT K_K] [CTRL ALT K_O] \
    	      [CTRL ALT K_P] [CTRL ALT K_Q] [CTRL ALT K_R] [CTRL ALT K_T] [CTRL ALT K_W] \
    	      [CTRL ALT K_COLON] [CTRL ALT K_EQUAL] [CTRL ALT K_COMMA] [CTRL ALT K_HYPHEN] [CTRL ALT K_PERIOD] \
    	      [CTRL ALT K_SLASH] [CTRL ALT K_BKQUOTE] [CTRL ALT K_LBRKT] [CTRL ALT K_BKSLASH] [CTRL ALT K_RBRKT] \
    	      [CTRL ALT K_QUOTE]

store(level3) U+007D U+200C U+0040 U+17D1 U+0024 \
              U+20AC U+17D9 U+17DA U+002A U+007B \ 
              U+179E U+17AF U+17A6 U+179D U+17B1 \
    	      U+17B0 U+17DC U+17AB U+17A8 U+17DD \
    	      U+17D6 U+17CE U+002C U+0078 U+002E \
    	      U+002F U+200D U+17A9 U+005C U+17B3 \
    	      U+17C8

store(input4) [SHIFT CTRL ALT K_0] [SHIFT CTRL ALT K_1] [SHIFT CTRL ALT K_2] [SHIFT CTRL ALT K_3] [SHIFT CTRL ALT K_4] \
              [SHIFT CTRL ALT K_5] [SHIFT CTRL ALT K_6] [SHIFT CTRL ALT K_7] [SHIFT CTRL ALT K_8] [SHIFT CTRL ALT K_9] \
              [SHIFT CTRL ALT K_A] [SHIFT CTRL ALT K_B] [SHIFT CTRL ALT K_C] [SHIFT CTRL ALT K_D] [SHIFT CTRL ALT K_E] \
              [SHIFT CTRL ALT K_F] [SHIFT CTRL ALT K_G] [SHIFT CTRL ALT K_H] [SHIFT CTRL ALT K_I] [SHIFT CTRL ALT K_J] \
              [SHIFT CTRL ALT K_K] [SHIFT CTRL ALT K_L] [SHIFT CTRL ALT K_M] [SHIFT CTRL ALT K_N] [SHIFT CTRL ALT K_O] \
    	      [SHIFT CTRL ALT K_P] [SHIFT CTRL ALT K_Q] [SHIFT CTRL ALT K_R] [SHIFT CTRL ALT K_S] [SHIFT CTRL ALT K_T] \
    	      [SHIFT CTRL ALT K_U] [SHIFT CTRL ALT K_V] [SHIFT CTRL ALT K_W] [SHIFT CTRL ALT K_X] [SHIFT CTRL ALT K_Y] \
    	      [SHIFT CTRL ALT K_Z] [SHIFT CTRL ALT K_COLON] [SHIFT CTRL ALT K_COMMA] [SHIFT CTRL ALT K_PERIOD] [SHIFT CTRL ALT K_LBRKT] \
    	      [SHIFT CTRL ALT K_RBRKT] [SHIFT CTRL ALT K_QUOTE] 

store(level4) U+17F0 U+17F1 U+17F2 U+17F3 U+17F4 \
              U+17F5 U+17F6 U+17F7 U+17F8 U+17F9 \
              U+19EC U+19FB U+19F9 U+19EE U+19E2 \
              U+19EF U+19F0 U+19F1 U+19E7 U+19F2 \
              U+19F3 U+19F4 U+19FD U+19FC U+19E8 \
    	      U+19E9 U+19E0 U+19E3 U+19ED U+19E4 \
    	      U+19E6 U+19FA U+19E1 U+19F8 U+19E5 \
    	      U+19F7 U+19F5 U+19FE U+19FF U+19EA \
    	      U+19EB U+19F6

store(input_subcons) \
  [T_17D2_1780] [T_17D2_1781] [T_17D2_1782] [T_17D2_1783] [T_17D2_1784] [T_17D2_1785] [T_17D2_1786] [T_17D2_1787] \
  [T_17D2_1788] [T_17D2_1789] [T_17D2_178A] [T_17D2_178B] [T_17D2_178C] [T_17D2_178D] [T_17D2_178E] [T_17D2_178F] \
  [T_17D2_1790] [T_17D2_1791] [T_17D2_1792] [T_17D2_1793] [T_17D2_1794] [T_17D2_1795] [T_17D2_1796] [T_17D2_1797] \
  [T_17D2_1798] [T_17D2_1799] [T_17D2_179A] [T_17D2_179B] [T_17D2_179C] [T_17D2_179D] [T_17D2_179E] [T_17D2_179F] \
  [T_17D2_17A0] [T_17D2_17A1] [T_17D2_17A2]

store(subcons) \
  U+1780 U+1781 U+1782 U+1783 U+1784 U+1785 U+1786 U+1787 \
  U+1788 U+1789 U+178A U+178B U+178C U+178D U+178E U+178F \
  U+1790 U+1791 U+1792 U+1793 U+1794 U+1795 U+1796 U+1797 \
  U+1798 U+1799 U+179A U+179B U+179C U+179D U+179E U+179F \
  U+17A0 U+17A1 U+17A2
  
store(vowel) \
  U+17B6 U+17B7 U+17B8 U+17B9 U+17BA U+17BB U+17BC U+17BD \
  U+17BE U+17BF U+17C0 U+17C1 U+17C2 U+17C3 U+17C4 U+17C5
store(vowel_second) \
  U+17C6 U+17C7         

store(trisap_muusikatoan) U+17C9 U+17CA 
  
store(prevowel) U+17C1 U+17C2 U+17C3 
store(prevowel_key) 'eES'

group(main) using keys

c Constraints

any(vowel) + "'" > context beep
U+17C6 + "'" > context beep
U+17C7 + "'" > context beep
U+17CB + "'" > context beep

c Rotation

any(prevowel) + any(prevowel_key) > index(prevowel, 2)

U+17C7 + 'H' > U+17C8
U+17C8 + 'H' > U+17C7

c Reordering - sala oum

U+17C6 + 'u' > U+17BB U+17C6

c Basic rules

+ any(input12) > index(level12,1)
+ any(input3) > index(level3,1)
+ any(input4) > index(level4,1)

+ [K_SPACE] > U+200B
+ [SHIFT K_SPACE] > U+0020
+ [CTRL ALT K_SPACE] > U+00A0   

+ "A" > U+17B6 U+17C6
+ "V" > U+17C1 U+17C7
+ ":" > U+17C4 U+17C7
+ "," > U+17BB U+17C6
+ "<" > U+17BB U+17C7  

U+17B6 U+17C6 + [K_BKSP] > nul
U+17C1 U+17C7 + [K_BKSP] > nul
U+17C4 U+17C7 + [K_BKSP] > nul
U+17BB U+17C6 + [K_BKSP] > nul
U+17BB U+17C7 + [K_BKSP] > nul

c Mobile/tablet touch layout

+ any(input_subcons) > U+17D2 index(subcons, 1)
platform('tablet') U+17D2 any(subcons) + [K_BKSP] > nul
platform('mobile') U+17D2 any(subcons) + [K_BKSP] > nul

+ [K_NPSTAR] > U+002A
+ [SHIFT K_NPSTAR] > U+002A
+ [K_NPPLUS] > U+002B
+ [SHIFT K_NPPLUS] > U+002B
+ [K_NPMINUS] > U+002D
+ [SHIFT K_NPMINUS] > U+002D 

match > use(normalise)

group(normalise)

c Normalise vowels typed separately

U+17C1 U+17B6 > U+17C4
U+17C1 U+17B8 > U+17BE

c Reordering - trisap and muusikatoan store before vowel

any(vowel) any(trisap_muusikatoan) > context(2) context(1)
any(trisap_muusikatoan) any(vowel) any(trisap_muusikatoan) > context(3) context(2)

c Rotate vowels, marks

any(vowel) any(vowel) > context(2) c beep
any(trisap_muusikatoan) any(trisap_muusikatoan) > context(2)

c Rotation of double vowels

any(vowel) any(vowel_second) any(vowel) > context(3)
any(vowel) any(vowel) any(vowel_second) > context(2) context(3)
any(vowel) any(vowel_second) any(vowel) any(vowel_second) > context(3) context(4)
any(vowel_second) any(vowel_second) > context(2)
any(vowel_second) any(vowel) > context(2) 
any(vowel) U+17B6 U+17C6 > context(2) context(3)
any(vowel) any(vowel_second) U+17B6 U+17C6 > context(3) context(4)
U+17B6 U+17C6 any(vowel) > context(3)
U+17B6 U+17C6 any(vowel) any(vowel_second) > context(3) context(4)
                
c Delete extraneous coeng markers

U+17D2 U+17D2 > context(1)
U+17D2 any(vowel) > context(2)
U+17D2 any(vowel_second) > context(2)

any(vowel) U+17D2 any(subcons) > context(2) context(3) context(1)
U+17BB U+17C6 U+17D2 any(subcons) > context(3) context(4) context(1) context(2)  

c EOF
